project.ext.lwjglVersion = "3.1.3"
project.ext.jomlVersion = "1.9.4"

buildscript {
    ext.kotlin_version = '1.1.3-2'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'application'

defaultTasks 'installApp'
mainClassName = "uk.co.nickthecoder.tickle.demo.DemoKt"

dependencies {
    compile project(':tickle-core')
    compile project(':tickle-editor')
    compile project(':tickle-groovy')
    compile project(':tickle-kotlin')
}

repositories {
    mavenCentral()
    mavenLocal()
}

import org.gradle.internal.os.OperatingSystem

switch (OperatingSystem.current()) {
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
}

subprojects {

    repositories {
        mavenCentral()
        mavenLocal()
    }

    version = '0.1'
    group = 'uk.co.nickthecoder'
}

project(':tickle-core') {

    apply plugin: 'kotlin'
    apply plugin: 'maven'

    defaultTasks 'install'

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

        compile 'com.eclipsesource.minimal-json:minimal-json:0.9.4'
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

        // See : https://www.lwjgl.org/customize

        compile "org.lwjgl:lwjgl:$lwjglVersion"
        compile "org.lwjgl:lwjgl-glfw:$lwjglVersion"
        compile "org.lwjgl:lwjgl-jemalloc:$lwjglVersion"
        compile "org.lwjgl:lwjgl-openal:$lwjglVersion"
        compile "org.lwjgl:lwjgl-opengl:$lwjglVersion"
        compile "org.lwjgl:lwjgl-stb:$lwjglVersion"
        compile "org.joml:joml:${jomlVersion}"

        runtime "org.lwjgl:lwjgl:$lwjglVersion:$lwjglNatives"
        runtime "org.lwjgl:lwjgl-glfw:$lwjglVersion:$lwjglNatives"
        runtime "org.lwjgl:lwjgl-jemalloc:$lwjglVersion:$lwjglNatives"
        runtime "org.lwjgl:lwjgl-openal:$lwjglVersion:$lwjglNatives"
        runtime "org.lwjgl:lwjgl-opengl:$lwjglVersion:$lwjglNatives"
        runtime "org.lwjgl:lwjgl-stb:$lwjglVersion:$lwjglNatives"

        compile "org.jbox2d:jbox2d-library:2.1.2.2"
        testCompile 'junit:junit:4.11'

    }
}

project(':tickle-editor') {

    apply plugin: 'kotlin'
    apply plugin: 'maven'

    defaultTasks 'install'

    dependencies {
        compile project(':tickle-core')

        compile 'uk.co.nickthecoder:paratask-core:0.2'
        compile 'org.reflections:reflections:0.9.11'

        // For kotlin scripting in the FX Coder.
        compile "org.jetbrains.kotlin:kotlin-script-util:$kotlin_version"
    }
}

project(':tickle-groovy') {

    apply plugin: 'kotlin'
    apply plugin: 'maven'

    defaultTasks 'install'

    dependencies {
        compile project(':tickle-core')
        compile 'org.codehaus.groovy:groovy-all:2.4.5'
    }
}

project(':tickle-kotlin') {

    apply plugin: 'kotlin'
    apply plugin: 'maven'

    defaultTasks 'install'

    dependencies {
        compile project(':tickle-core')
        compile "org.jetbrains.kotlin:kotlin-script-util:$kotlin_version"
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
